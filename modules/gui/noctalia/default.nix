{ inputs, lib, ... }:
let
  inherit (lib)
    foldl'
    getExe
    getExe'
    isFunction
    mkForce
    mkOption
    mkOptionType
    optionalString
    ;
in
{
  flake.nixosModules.core = {
    options.custom = {
      programs.noctalia = {
        # reducer functions are used instead of plain attrsets, as attrsets cannot be merged together to override
        # lists at arbitrary indexes
        settingsReducers = mkOption {
          type = lib.types.listOf (mkOptionType {
            name = "noctalia-settings-reducer";
            check = isFunction;
          });
          default = [ ];
          description = "Reducers that will be applied to a copy of desktop's gui-settings.json";
        };
      };
    };
  };

  flake.nixosModules.wm =
    {
      config,
      isLaptop,
      pkgs,
      ...
    }:
    let
      # settings.json is the desktop copy of gui-settings.json without any modifications
      defaultSettings = builtins.fromJSON (builtins.readFile ./settings.json);
      noctalia-shell' =
        (inputs.noctalia.packages.${pkgs.stdenv.hostPlatform.system}.default.override {
          calendarSupport = true;
        }).overrideAttrs
          {
            patches = [
              ./face-aware-crop.patch
              # battery and volume widgets that use the primary color instead of white
              ./mprimary-bar-widgets.patch
            ];

            # don't want to add python3 to the global path
            postPatch = ''
              substituteInPlace Services/Theming/TemplateProcessor.qml \
                --replace-fail "python3" "${getExe pkgs.python3}"
            '';
          };
      # wrapped noctalia ipc to automatically kill outdated instances of noctalia-shell and restart
      noctalia-ipc = pkgs.writeShellApplication {
        name = "noctalia-ipc";
        runtimeInputs = with pkgs; [
          killall
          jq
        ];
        text = /* sh */ ''
          RAW_OUTPUT=$(noctalia-shell list --all --json 2>/dev/null)

          # invalid json, no instances running, so start noctalia-shell
          if [[ ! "$RAW_OUTPUT" == "["* ]]; then
            systemctl --user restart noctalia-shell
            exit
          fi

          NOCTALIA_PATH=$(noctalia-shell list --all --json | jq -r '.[] | .config_path | sub("/share/noctalia-shell/shell.qml$"; "")')

          # using dev version, don't kill the shell
          if [[ "$NOCTALIA_PATH" =~ "_dirty" ]]; then
            "$NOCTALIA_PATH/bin/noctalia-shell" ipc call "$@"
            exit
          fi

          # different instance, kill previous instances
          if [[ ! "$NOCTALIA_PATH" =~ "${pkgs.noctalia-shell}" ]]; then
            killall .quickshell-wrapper
            systemctl --user restart noctalia-shell
            sleep 2
          fi

          ${lib.getExe pkgs.noctalia-shell} ipc call "$@"
        '';
      };
    in
    {
      nixpkgs.overlays = [
        (_: _prev: {
          noctalia-shell = noctalia-shell';
        })
      ];

      hj.xdg.config.files =

        let
          official = "https://github.com/noctalia-dev/noctalia-plugins";
          my-plugins = "https://github.com/iynaix/noctalia-plugins-iynaix";
          my-plugins-hash = my-plugins |> builtins.hashString "sha256" |> builtins.substring 0 6;
        in
        {
          "noctalia/settings.json".text =
            # create settings by applying reducers
            config.custom.programs.noctalia.settingsReducers
            |> foldl' (curr: reducer: reducer curr) defaultSettings
            |> lib.strings.toJSON;
          "noctalia/plugins.json".text = lib.strings.toJSON {
            sources = [
              {
                enabled = true;
                name = "Official Noctalia Plugins";
                url = official;
              }
              {
                enabled = true;
                name = "Iynaix's Noctalia Plugins";
                url = my-plugins;
              }
            ];
            states = {
              # official plugins
              kaomoji-provider = {
                enabled = true;
                sourceUrl = official;
              };
              screen-recorder = {
                enabled = true;
                sourceUrl = official;
              };
              timer = {
                enabled = true;
                sourceUrl = official;
              };
              # third party plugins (hash generated by noctalia)
              "${my-plugins-hash}:projects-provider" = {
                enabled = true;
                sourceUrl = my-plugins;
              };
            };
            version = 2;
          };
          "noctalia/plugins/${my-plugins-hash}:projects-provider/settings.json".text = lib.strings.toJSON {
            projectDir = "~/projects";
            openCommand = "codium %s";
          };
        };

      # custom noctalia service that starts after the WM is ready
      # don't use flake's systemd service, it's very buggy :(
      systemd.user.services = {
        noctalia-shell = {
          description = "Noctalia Shell - Wayland desktop shell";
          documentation = [ "https://docs.noctalia.dev/docs" ];
          partOf = [ "graphical-session.target" ];
          # this shit doesn't work because nixos doesn't properly restart user services
          # https://github.com/NixOS/nixpkgs/issues/246611#issuecomment-3342453760
          restartTriggers = [ noctalia-shell' ];

          # fix runtime deps when starting noctalia-shell from systemd
          # https://github.com/noctalia-dev/noctalia-shell/pull/418
          environment = {
            PATH = mkForce "/run/wrappers/bin:/etc/profiles/per-user/%u/bin:/nix/var/nix/profiles/default/bin:/run/current-system/sw/bin";
          };

          serviceConfig = {
            ExecStart = lib.getExe noctalia-shell';
            Restart = "on-failure";
          };
        };

        # run wallpaper after noctalia-shell starts
        wallpaper = {
          wantedBy = [ "noctalia-shell.service" ];

          unitConfig = {
            Description = "Set the wallpapers";
            After = [ "noctalia-shell.service" ];
          };

          serviceConfig = {
            Type = "oneshot";
            ExecStartPre = "${getExe' pkgs.coreutils "sleep"} 3";
            ExecStart = getExe (
              pkgs.writeShellApplication {
                name = "wallpaper-startup";
                runtimeInputs = [
                  noctalia-shell'
                  config.custom.programs.dotfiles-rs
                ];
                text = ''
                  # hide on laptop screens to save space
                  ${optionalString isLaptop "noctalia-shell ipc call bar hide"}
                  wallpaper
                '';
              }
            );
            # ensures systemd considers it "active" even after the script finishes
            # this prevents it from restarting again when noctalia-shell restarts after boot
            RemainAfterExit = "yes";
          };
        };
      };

      environment.systemPackages = [
        noctalia-shell'
        noctalia-ipc
        pkgs.gpu-screen-recorder # screen recorder plugin
      ];

      # start after WM initializes
      custom.startupServices = [ "noctalia-shell.service" ];

      custom.shell.packages = {
        inherit noctalia-ipc;
        noctalia-shell-reload = {
          text = /* sh */ ''
            systemctl --user restart noctalia-shell
          '';
        };
        noctalia-diff = {
          runtimeInputs = with pkgs; [
            jq
            colordiff
            wl-clipboard
          ];
          text = /* sh */ ''
            diff -u <(jq -S . ${config.hj.xdg.config.directory}/noctalia/settings.json) <(wl-paste | jq -S .) | colordiff
          '';
        };
      };

      custom.programs = {
        # add noctalia-ipc to dotfiles-rs, so wallpaper can find it on boot
        dotfiles-rs = pkgs.custom.dotfiles-rs.override { extraPackages = [ noctalia-ipc ]; };
        # setup blur for hyprland
        hyprland.settings = {
          layerrule = [
            "match:namespace noctalia-background-.*$, ignore_alpha 0.5, blur on"
          ];
        };
      };

      custom.persist = {
        home = {
          directories = [
            ".config/noctalia"
          ];

          # wallpapers.json contains the last set wallpaper
          cache.directories = [
            ".cache/noctalia"
          ];
        };
      };
    };
}
