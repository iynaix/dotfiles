{
  config,
  lib,
  ...
}:
let
  inherit (lib)
    mapAttrs'
    mkForce
    mkIf
    mkMerge
    nameValuePair
    ;
  opacity = "E5"; # 90%
  # NOTE: real dunst config is read from here
  actualDunstConfig = "${config.xdg.cacheHome}/wallust/dunstrc";
in
mkIf (config.custom.wm != "tty") (mkMerge [
  {
    services.dunst = {
      enable = true;
      # NOTE: dunst settings are generated in ~/.config/dunst/dunstrc, which wallust uses as a template
      # dunst is then started using the processed file (~/.cache/wallust/dunstrc) instead
      configFile = actualDunstConfig;
      settings = {
        global = {
          browser = "brave -new-tab";
          corner_radius = 8;
          dmenu = "rofi -p dunst:";
          enable_recursive_icon_lookup = true;
          ellipsize = "end";
          follow = "mouse";
          font = "${config.custom.fonts.regular} 12";
          frame_color = "{{background}}";
          frame_width = 0;
          horizontal_padding = 10;
          # icon_theme will be read from $XDG_DATA_HOME/icons, these are symlinked in gtk.nix
          icon_theme = config.gtk.iconTheme.name;
          icon_path = mkForce "";
          max_icon_size = 72;
          mouse_left_click = "do_action";
          mouse_middle_click = "do_action";
          mouse_right_click = "close_current";
          separator_color = "{{color7}}";
          separator_height = 1;
          show_indicators = "no";
        };

        urgency_critical = {
          background = "{{color1}}";
          foreground = "{{foreground}}";
          timeout = 0;
        };

        urgency_low = {
          background = "{{background}}${opacity}";
          foreground = "{{foreground}}";
          timeout = 10;
        };

        urgency_normal = {
          background = "{{background}}${opacity}";
          foreground = "{{foreground}}";
          timeout = 10;
        };
      };
    };

    # show dunst history
    wayland.windowManager.hyprland.settings.bind = [
      "$mod, n, exec, dunstctl history-pop"
    ];

    programs.niri.settings.binds = {
      "Mod+n".action.spawn = [
        "dunstctl"
        "history-pop"
      ];
    };

    # wait for colorscheme to be ready on boot
    systemd.user.services.dunst = {
      Unit.AssertPathExists = [ actualDunstConfig ];
    };
  }

  (mkIf config.services.dunst.enable {
    # create symlink in $XDG_DATA_HOME/.icons for each icon accent variant
    # allows dunst to be able to refer to icons by name
    xdg.dataFile = mapAttrs' (
      accent: _:
      let
        iconTheme = "Tela-${accent}-dark";
      in
      nameValuePair "icons/${iconTheme}" {
        source = "${config.gtk.iconTheme.package}/share/icons/${iconTheme}";
      }
    ) config.custom.gtk.accents;

    custom.wallust.templates.dunstrc = {
      # use the text generated by home-manager
      text = "${config.xdg.configHome}/dunst/dunstrc";
      target = actualDunstConfig;
    };
  })
])
